;HTWAM3 test code

!source "stdlib/stdlib.a"
!to "HTWAM.prg", cbm
!sl "HTWAM.map"
!sal
!svl "HTWAM.lbl"
!pdb "HTWAM.pdb"
!cpu 6510
!ct pet
!initmem $aa

kMaxSprites = 24

!macro MIRQStack {
	pha
	txa
	pha
	tya
	pha
	inc VIC2BorderColour
}
!macro MIRQStackPop {
	dec VIC2BorderColour
	pla
	tay
	pla
	tax
	pla
	rti
}


*=$400
!fill 40*25 , ' '

*=$800
.bph
	sei
	lda #ProcessorPortAllRAMWithIO
	jsr InitialiseMachine
	lda #VIC2ScreenControlVDefault
	sta VIC2ScreenControlV

	+MVIC2SpriteEnableAll_A

	+MWordValueToAddress_A .firstRow , KERNALIRQServiceRoutineLo
	lda #20
	sta VIC2Raster
	lda #1
	sta VIC2InteruptControl
	cli

.ml1
	inc SCREENRAM
	jmp .ml1


.firstRow
	+MIRQStack

!for .ii , kVIC2NumSprites {
!set .i = .ii - 1
	ldx SpriteIndexTab + .i
	lda SpriteXTab,x
	sta VIC2Sprite0X + (.i * 2)
	lda SpriteYTab,x
	sta VIC2Sprite0Y + (.i * 2)
	lda SpriteColourTab,x
	sta VIC2Sprite0Colour + .i
	lda SpriteFrameTab,x
	sta SCREENRAM + SPRITEFRAMESCROFF + .i
}

	lda #0
	sta .nextVICSpriteIndex
	lda #8
	sta .nextSpriteIndexToProcess

	+MACROAckRasterIRQ_A
	; Calculate the first safe raster position, underneath the first sprite Y position
	lda VIC2Sprite0Y
	clc
	adc #22
	cmp #VIC2SpriteYBorderTop
	bcs .okPosition
	lda #VIC2SpriteYBorderTop
.okPosition
	sta VIC2Raster
	+MWordValueToAddress_A .secondRow , KERNALIRQServiceRoutineLo

	+MIRQStackPop

.nextVICSpriteIndex !by 0
.nextSpriteIndexToProcess !by 0

.secondRow
	+MIRQStack

	lda .nextVICSpriteIndex
	asl
	tay

.safeToProcessSprite
	ldx .nextSpriteIndexToProcess
	lda SpriteIndexTab,x
	tax
	lda SpriteXTab,x
	sta VIC2Sprite0X,y
	lda SpriteYTab,x
	sta VIC2Sprite0Y,y
	ldy .nextVICSpriteIndex
	lda SpriteColourTab,x
	sta VIC2Sprite0Colour,y
	lda SpriteFrameTab,x
	sta SCREENRAM + SPRITEFRAMESCROFF,y

	inc .nextSpriteIndexToProcess
	lda .nextSpriteIndexToProcess
	cmp #kMaxSprites
	bcs .startTopRowAgain

	iny
	tya
	and #7
	sta .nextVICSpriteIndex

	asl
	tay
	; Test the next sprite Y position bottom edge with the current raster
	lda VIC2Sprite0Y,y
	clc
	adc #22
	sta .nextSafePosition
	lda VIC2Raster
	cmp .nextSafePosition
	bcs .safeToProcessSprite

	; Is the next safe position very close in terms of raster time?
!if 1 {
.closeTime
	lda .nextSafePosition
	sec
	sbc VIC2Raster
	cmp #2
	bcs .notCloseTime

.quickWait
;	inc VIC2ScreenColour
	lda VIC2Raster
	cmp .nextSafePosition
	bcc .quickWait
	jmp .safeToProcessSprite
}

; The next raster is not near, so trigger it later
.notCloseTime
	+MACROAckRasterIRQ_A
	lda .nextSafePosition
	sta VIC2Raster

	+MIRQStackPop

.nextSafePosition !by 0

.startTopRowAgain

	inc SpriteYTab
	inc SpriteYTab
	inc SpriteYTab+5
	inc SpriteYTab+10
	inc SpriteYTab+15
	inc SpriteYTab+20
	inc SpriteYTab+20

	inc VIC2BorderColour
!if 0 {
.doSort
	ldy #0
	sty .changedFlag
.sortLoop1
	ldx SpriteIndexTab+1,y
	lda SpriteYTab,x
	ldx SpriteIndexTab,y
	cmp SpriteYTab,x
	bcs .isGreaterEQThan

	; Swap index
	lda SpriteIndexTab+1,y
	pha
	lda SpriteIndexTab,y
	sta SpriteIndexTab+1,y
	pla
	sta SpriteIndexTab,y
	inc .changedFlag

.isGreaterEQThan
	iny
	cpy #kMaxSprites-2
	bne .sortLoop1

	lda .changedFlag
	bne .doSort
}

!if 0 {
	; Unrolled version
.doSort
	lda #0
	sta .changedFlag
!for ii , kMaxSprites-2 {
!set .i = ii - 1
	ldx SpriteIndexTab+1+.i
	lda SpriteYTab,x
	ldy SpriteIndexTab+.i
	cmp SpriteYTab,y
	bcs +

	; Swap index
	stx SpriteIndexTab+.i
	sty SpriteIndexTab+1+.i
	inc .changedFlag

+
}
	lda .changedFlag
	beq .odoSort
	jmp .doSort
.odoSort
}

!if 1 {
	; Unrolled version with backwards check
.doSort
!set .previousBlock = 0
!for ii , kMaxSprites-2 {
!set .i = ii - 1
!set .currentBlock = *
	ldx SpriteIndexTab+1+.i
	lda SpriteYTab,x
	ldy SpriteIndexTab+.i
	cmp SpriteYTab,y
	bcs +

	; Swap index
	stx SpriteIndexTab+.i
	sty SpriteIndexTab+1+.i
!if .previousBlock > 0 {
	jmp .previousBlock
}

+
!set .previousBlock = .currentBlock
}
}

	dec VIC2BorderColour


	+MWordValueToAddress_A .firstRow , KERNALIRQServiceRoutineLo
	+MACROAckRasterIRQ_A
	lda #20
	sta VIC2Raster
	+MIRQStackPop

.changedFlag !by 0

SpriteIndexTab
!for .ii , kMaxSprites {
!set .i = .ii-1
	!by .i
}

SpriteXTab
!for .ii , kMaxSprites {
!set .i = .ii-1
	!by 30 + (.i * 9)
}

SpriteYTab
	!by 50 , 50 , 50 , 50 , 51 , 51 , 60 , 72
	!by 80 , 100 , 100 , 110 , 120 , 130 , 140 , 150
	!by 160 , 170 , 180 , 190 , 200 , 220 , 220 , 230



SpriteColourTab
!for .i , kMaxSprites {
!set .i = .i & 15
!if .i < 1 {
!set .i = 1
}
	!by .i
}

SpriteFrameTab
!for .ii , kMaxSprites {
!set .i = .ii-1
	!by 248 + (.i & 7)
}
	

!source "stdlib/Initialise.a"

*=$3e00
+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %####.......##.......####
+SpriteLine %###.#......##......#.###
+SpriteLine %###..#.....##.....#..###
+SpriteLine %###...#....##....#...###
+SpriteLine %###....#...##...#....###
+SpriteLine %###.....#..##..#.....###
+SpriteLine %###......######......###
+SpriteLine %###......#####.......###
+SpriteLine %########################
+SpriteLine %###......#####.......###
+SpriteLine %###......#####.......###
+SpriteLine %###.....#..#..#......###
+SpriteLine %###....#...#...#.....###
+SpriteLine %###...#....#....#....###
+SpriteLine %###..#.....#.....#...###
+SpriteLine %###.#......#......#..###
+SpriteLine %####.......#.......#.###
+SpriteLine %########################
+SpriteLine %########################
!by 0

+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %###........##.......####
+SpriteLine %###........##......#.###
+SpriteLine %###........##.....#..###
+SpriteLine %###........##....#...###
+SpriteLine %###........##...#....###
+SpriteLine %###........##..#.....###
+SpriteLine %###......######......###
+SpriteLine %###......#####.......###
+SpriteLine %########################
+SpriteLine %###......#####.......###
+SpriteLine %###......#####.......###
+SpriteLine %###.....#..#..#......###
+SpriteLine %###....#...#...#.....###
+SpriteLine %###...#....#....#....###
+SpriteLine %###..#.....#.....#...###
+SpriteLine %###.#......#......#..###
+SpriteLine %####.......#.......#.###
+SpriteLine %########################
+SpriteLine %########################
!by 0

+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %###.................####
+SpriteLine %###................#.###
+SpriteLine %###...............#..###
+SpriteLine %###..............#...###
+SpriteLine %###.............#....###
+SpriteLine %###............#.....###
+SpriteLine %###......######......###
+SpriteLine %###......#####.......###
+SpriteLine %########################
+SpriteLine %###......#####.......###
+SpriteLine %###......#####.......###
+SpriteLine %###.....#..#..#......###
+SpriteLine %###....#...#...#.....###
+SpriteLine %###...#....#....#....###
+SpriteLine %###..#.....#.....#...###
+SpriteLine %###.#......#......#..###
+SpriteLine %####.......#.......#.###
+SpriteLine %########################
+SpriteLine %########################
!by 0

+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %####.......##........###
+SpriteLine %###.#......##........###
+SpriteLine %###..#.....##........###
+SpriteLine %###...#....##........###
+SpriteLine %###....#...##........###
+SpriteLine %###.....#..##........###
+SpriteLine %###......####........###
+SpriteLine %###......####........###
+SpriteLine %##############.......###
+SpriteLine %###......#####.......###
+SpriteLine %###......#####.......###
+SpriteLine %###.....#..#..#......###
+SpriteLine %###....#...#...#.....###
+SpriteLine %###...#....#....#....###
+SpriteLine %###..#.....#.....#...###
+SpriteLine %###.#......#......#..###
+SpriteLine %####.......#.......#.###
+SpriteLine %########################
+SpriteLine %########################
!by 0

+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %####.......##.......####
+SpriteLine %###.#......##......#.###
+SpriteLine %###..#.....##.....#..###
+SpriteLine %###...#....##....#...###
+SpriteLine %###....#...##...#....###
+SpriteLine %###.....#..##..#.....###
+SpriteLine %###......######......###
+SpriteLine %###......#####.......###
+SpriteLine %########################
+SpriteLine %###..................###
+SpriteLine %###..................###
+SpriteLine %###........#..#......###
+SpriteLine %###........#...#.....###
+SpriteLine %###........#....#....###
+SpriteLine %###........#.....#...###
+SpriteLine %###........#......#..###
+SpriteLine %###........#.......#.###
+SpriteLine %########################
+SpriteLine %########################
!by 0

+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %####.......##.......####
+SpriteLine %###.#......##......#.###
+SpriteLine %###..#.....##.....#..###
+SpriteLine %###...#....##....#...###
+SpriteLine %###....#...##...#....###
+SpriteLine %###.....#..##..#.....###
+SpriteLine %###......######......###
+SpriteLine %###......#####.......###
+SpriteLine %########################
+SpriteLine %###......###.........###
+SpriteLine %###......##..........###
+SpriteLine %###.....#............###
+SpriteLine %###....#.............###
+SpriteLine %###...#..............###
+SpriteLine %###..#...............###
+SpriteLine %###.#................###
+SpriteLine %####.................###
+SpriteLine %########################
+SpriteLine %########################
!by 0

+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %####................####
+SpriteLine %###.#..............#.###
+SpriteLine %###..#............#..###
+SpriteLine %###...#..........#...###
+SpriteLine %###....#........#....###
+SpriteLine %###.....#......#.....###
+SpriteLine %###......######......###
+SpriteLine %###......#####.......###
+SpriteLine %########################
+SpriteLine %###......#####.......###
+SpriteLine %###......#####.......###
+SpriteLine %###.....#.....#......###
+SpriteLine %###....#.......#.....###
+SpriteLine %###...#.........#....###
+SpriteLine %###..#...........#...###
+SpriteLine %###.#.............#..###
+SpriteLine %####...............#.###
+SpriteLine %########################
+SpriteLine %########################
!by 0

+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %####.......##.......####
+SpriteLine %###.#......##......#.###
+SpriteLine %###..#.....##.....#..###
+SpriteLine %###...#....##....#...###
+SpriteLine %###....#...##...#....###
+SpriteLine %###.....#..##..#.....###
+SpriteLine %###......######......###
+SpriteLine %###..................###
+SpriteLine %###..................###
+SpriteLine %###..................###
+SpriteLine %###......#####.......###
+SpriteLine %###.....#..#..#......###
+SpriteLine %###....#...#...#.....###
+SpriteLine %###...#....#....#....###
+SpriteLine %###..#.....#.....#...###
+SpriteLine %###.#......#......#..###
+SpriteLine %####.......#.......#.###
+SpriteLine %########################
+SpriteLine %########################
!by 0

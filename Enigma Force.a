!source "stdlib/stdlib.a"
!source "stdlib/LongBranches.a"
!to "Enigma Force 4000.prg", cbm
!sal
!sl "Enigma Force 4000.map"
!svl "Enigma Force 4000.lbl"
!pdb "Enigma Force 4000.pdb"
!cpu 6510
!ct pet

* = $200
!bin "c:\temp\default start.prg" , $140 , 2

kRangeClampX = 48
kRangeClampXM = 60
kRangeClampY = 48
kRangeClampYM = 110

* = $340
	ldx #0
	lda #%00111111
	sta CIA1PortADDR
	lda #0
	sta CIA1KeyboardColumnJoystickA
	jmp $a144

* = $350
	lda #%11000000
	sta CIA1PortADDR
	lda #%01000000
	sta CIA1KeyboardColumnJoystickA
	lda SIDPaddleX
	and #%01111110
	asl
	pha
	sec
	sbc .previousX
	+M_ASR
	sta .delta
	pla
	sta .previousX
	lda .accurateX
	clc
	adc .delta
	cmp #kRangeClampX+1
	bcs .o1
	dec $a8
	dec $a8
	lda #kRangeClampX
.o1
	cmp #256-kRangeClampXM
	bcc .o2
	inc $a8
	inc $a8
	lda #256-kRangeClampXM
.o2
	sta .accurateX
	sec
	sbc #kRangeClampX
	clc
	adc #12	; Final tweak for screen pos
	sta $f463

	lda SIDPaddleY
	and #%01111110
	asl
	pha
	sec
	sbc .previousY
	+M_ASR
	sta .delta
	pla
	sta .previousY
	lda .accurateY
	sec
	sbc .delta
	cmp #kRangeClampY+1
	bcs .o3
	lda #kRangeClampY
.o3
	cmp #256-kRangeClampYM
	bcc .o4
	lda #256-kRangeClampYM
.o4
	sta .accurateY
	; Now convert into screen space
	sec
	sbc #kRangeClampY
	clc
	adc #$8e	; Final tweak for screen pos
	sta $f4df
	lda CIA1KeyboardRowsJoystickB
	and #%10000
	eor #%10000
	sta $ab
	lda #0
	sta CIA1PortADDR
	jmp $a10e
.previousX !by 0
.previousY !by 0
.delta !by 0
.accurateX !by 0
.accurateY !by 0

* = SCREENRAM
!scr "1541 mouse port 1 code by martin piper"
!align 1023 , 0 , $20


* = $800
!bin "c:\temp\enigmaforce 4000.prg" , , 2

!disablesegmentcheck
* = $a13b
	jsr $340
* = $9db4
	jsr $350
!enablesegmentcheck

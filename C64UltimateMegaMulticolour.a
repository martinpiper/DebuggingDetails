; C64Ultimate demonstrates mega multicolour mode (MMM).
; Execute at 64MHz without badlines.
; ..\c64\acme.exe --lib ../BDD6502/ --lib ../C64/ -v9 -f cbm -o C64UltimateMegaMulticolour.prg C64UltimateMegaMulticolour.a && ..\c64\bin\LZMPi.exe -pp $35 -c64mbu C64UltimateMegaMulticolour.prg C64UltimateMegaMulticolour.prg $e000 && curl --request POST --data-binary "@C64UltimateMegaMulticolour.prg" http://c64u/v1/runners:run_prg http://c64u/v1/runners:run_prg

; To reset the machine
; curl --request PUT http://c64u/v1/machine:reset

; To setup the machine for 64MHz manual control with bad lines disabled:
; curl --request PUT http://c64u/v1/configs/U64%20Specific%20Settings/CPU%20Speed?value=64
; curl --request PUT http://c64u/v1/configs/U64%20Specific%20Settings/Turbo%20Control?value=Manual
; curl --request PUT http://c64u/v1/configs/U64%20Specific%20Settings/Badline%20Timing?value=Disabled

; To convert images:
; C:\Downloads\ImageMagick-7.0.7-4-portable-Q16-x64\magick.exe "C:\Users\marti\Pictures\AH_KingTut_aga_cropped.png" -resize 256x200! -remap "C:\work\c64\CharPack\Palette.bmp" c:\temp\out.png
; C:\Downloads\ImageMagick-7.0.7-4-portable-Q16-x64\magick.exe "C:\Users\marti\Pictures\AH_Eye resized.png" -resize 256x200! -remap "C:\work\c64\CharPack\Palette.bmp" c:\temp\out.png
; C:/Work/C64/SimpleImageConvert/main.py C:\work\c64\CharPack\Palette.bmp c:\temp\out.png c:\temp\out.bin

!initmem ' '

!source "stdlib/stdlib.a"
!source "stdlib/LongBranches.a"

* = $200
+SpriteLine %........................
+SpriteLine %.#......................
+SpriteLine %.##.....................
+SpriteLine %.###....................
+SpriteLine %.####...................
+SpriteLine %.#####..................
+SpriteLine %.######.................
+SpriteLine %.#######................
+SpriteLine %.########...............
+SpriteLine %.#########..............
+SpriteLine %.########...............
+SpriteLine %.######.................
+SpriteLine %.######.................
+SpriteLine %.##..##.................
+SpriteLine %.#....##................
+SpriteLine %......##................
+SpriteLine %.......##...............
+SpriteLine %.......##...............
+SpriteLine %........##..............
+SpriteLine %........##..............
+SpriteLine %........................
!byte 0

+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %###..................###
+SpriteLine %###..................###
+SpriteLine %###..................###
+SpriteLine %###..................###
+SpriteLine %###..................###
+SpriteLine %###..................###
+SpriteLine %###.......###........###
+SpriteLine %###......#####.......###
+SpriteLine %###......#####.......###
+SpriteLine %###......#####.......###
+SpriteLine %###.......###........###
+SpriteLine %###..................###
+SpriteLine %###..................###
+SpriteLine %###..................###
+SpriteLine %###..................###
+SpriteLine %###..................###
+SpriteLine %###..................###
+SpriteLine %########################
+SpriteLine %########################
!byte 0

+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %####.......##.......####
+SpriteLine %###.#......##......#.###
+SpriteLine %###..#.....##.....#..###
+SpriteLine %###...#....##....#...###
+SpriteLine %###....#...##...#....###
+SpriteLine %###.....#..##..#.....###
+SpriteLine %###......######......###
+SpriteLine %###......#####.......###
+SpriteLine %########################
+SpriteLine %###......#####.......###
+SpriteLine %###......#####.......###
+SpriteLine %###.....#..#..#......###
+SpriteLine %###....#...#...#.....###
+SpriteLine %###...#....#....#....###
+SpriteLine %###..#.....#.....#...###
+SpriteLine %###.#......#......#..###
+SpriteLine %####.......#.......#.###
+SpriteLine %########################
+SpriteLine %########################
!byte 0

+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %########################
+SpriteLine %########################
!byte 0

* = SCREENRAM + (VIC2ScreenCharsWidth * 0)
	!scr "c64ultimate mega multicolour mode (mmm)"
	
* = SCREENRAM + (VIC2ScreenCharsWidth * 1)
	!fill 40 , '*'


* = SCREENRAM + (VIC2ScreenCharsWidth * 2)
	!scr "this is the normal screen"

* = SCREENRAM + (VIC2ScreenCharsWidth * 8) + 5
	!scr "nothing special here"

* = SCREENRAM + (VIC2ScreenCharsWidth * 10) + 10
	!scr "apart from the background"

* = SCREENRAM + (VIC2ScreenCharsWidth * 15)
	!scr "background has all 16 colours"
* = SCREENRAM + (VIC2ScreenCharsWidth * 16)
	!scr "without restrictions"
* = SCREENRAM + (VIC2ScreenCharsWidth * 18)
	!scr "and it can scroll"

* = SCREENRAM + (VIC2ScreenCharsWidth * 24) + 2
	!scr "code by martin piper"

; Sprite pointers, using the code as sprite frame data
* = $7f8
	!by 8,9,10,11,8,9,10,11

* = $e000
	sei
	clc
	cld
	ldx #0
.clr1
	txa
	sta COLOURRAM + VIC2ScreenCharsWidth,x
	sta COLOURRAM + $100,x
	sta COLOURRAM + $200,x
	sta COLOURRAM + $300,x
	inx
	bne .clr1

	; Init Apex music
	lda #<label_837d
	ldx #>label_837d
	jsr label_08ce


	; Setup sprites to be visible
	lda #%11111111
;	lda #0
	sta VIC2SpriteEnable
	; Sprites Y pos
	lda #100
!for .i , 4 {
	sta VIC2Sprite0Y + (.i-1)*2
	sta VIC2Sprite4Y + (.i-1)*2
	adc #21
}
	lda #VIC2Colour_Green
	ldx #VIC2Colour_White
!for .i , 4 {
	sta VIC2Sprite0Colour + (.i-1) * 2
	stx VIC2Sprite1Colour + (.i-1) * 2
}
	
	lda #20
!for .i , 4 {
	sta VIC2Sprite0X + (.i-1)*2
	adc #8
}
	lda #22
!for .i , 4 {
	sta VIC2Sprite4X + (.i-1)*2
	adc #8
}
;	lda #%11110000
	lda #0
	sta VIC2SpriteXMSB
	jmp .l1
	
!align 255 , 0 
.l1
	+MByteValueToAddress_A VIC2Colour_Blue , VIC2BorderColour
.l12
	lda VIC2ScreenControlV
	bmi .l12
	+MWordValueToAddress_A MegaScreenData , $fb
	+MByteValueToAddress_A VIC2SpriteYBorderTop + 9, $fd
	+MByteValueToAddress_A >MegaScreenData, $fe
	+MByteValueToAddress_A 0, $fb
	jsr CalcLinePosY
.l2
	lda $fd
	jsr WaitRaster

.smpx = *+1
	lda #0
	clc
	adc $fd
	tay
	lda SinTab,y
	sec
	adc .smpx
	tay

	; Even though "bad lines" are turned off, there seems to be 40 cycles at 64 MHz used for the line fetch.
	; At least it's not 40 cycles at 1MHz, which would be much longer in terms of time.
	and #%111
	cmp #3
	beq .isBadLine

	jsr RenderMegaLine
	jmp .l3

.isBadLine

	jsr RenderMegaLineBad

.l3
;	+MAddU8ToAddr16 160 , $fb , $fc
	inc $fc

	jsr CalcLinePosY
	jsr CalcSpriteLine

	inc $fd
	lda $fd
	cmp #VIC2SpriteYBorderBottom - 8
	bne .l2
	
!if >* != >.l1 {
	!error ".l1 to here must be in the same page"
}

	+MACROWaitForTheLastScan_A

	inc VIC2BorderColour
	; Play Apex music
	jsr label_093d

	inc VIC2BorderColour

	inc VIC2BorderColour
	inc .scrollXPos
	lda .scrollXPos
	and #7
	sta .scrollXPos
	sta VIC2ScreenControlH
	+lbne .scl1
!for .yy , VIC2ScreenCharsHeight {
!set .y = .yy-1
	lda SCREENRAM + (.y * VIC2ScreenCharsWidth) + VIC2ScreenCharsWidth-1
	pha
	lda COLOURRAM + (.y * VIC2ScreenCharsWidth) + VIC2ScreenCharsWidth-1
	pha
	ldx #VIC2ScreenCharsWidth-2
!set .csl1 = *
	lda SCREENRAM + (.y * VIC2ScreenCharsWidth),x
	sta SCREENRAM + (.y * VIC2ScreenCharsWidth)+1,x
	lda COLOURRAM + (.y * VIC2ScreenCharsWidth),x
	sta COLOURRAM + (.y * VIC2ScreenCharsWidth)+1,x
	dex
	bpl .csl1
	pla
	sta COLOURRAM + (.y * VIC2ScreenCharsWidth)
	pla
	sta SCREENRAM + (.y * VIC2ScreenCharsWidth)
}
.scl1
	
	inc .smpx
	dec .smtp1
	
	jmp .l1

.scrollXPos !by 0

CalcLinePosY
	lda .smpx
	clc
	adc $fe
	tay
	lda SinTab2,y
	clc
	adc $fe
	cmp #(>MegaScreenData)+200
	bcc .ok1
	sec
	sbc #(>MegaScreenData)+200
.ok1
	cmp #>MegaScreenData
	bcs .ok2
	clc
	adc #>MegaScreenData
.ok2
	sta $fc
	inc $fe
	rts

CalcSpriteLine
	lda $fd
	and #%11
	bne .notYet
	lda $fd
	clc
	adc #1
	sta VIC2Sprite0Y
	sta VIC2Sprite1Y
	sta VIC2Sprite2Y
	sta VIC2Sprite3Y
	sta VIC2Sprite4Y
	sta VIC2Sprite5Y
	sta VIC2Sprite6Y
	sta VIC2Sprite7Y
.notYet
	; Update all sprite colours, every line
	lda $fd
	sta VIC2Sprite0Colour
	sta VIC2Sprite1Colour
	sta VIC2Sprite2Colour
	sta VIC2Sprite3Colour
	eor #$ff
	sta VIC2Sprite4Colour
	sta VIC2Sprite5Colour
	sta VIC2Sprite6Colour
	sta VIC2Sprite7Colour
	
	lda #0
	sta $ff
.smtp1 = *+1
	lda #0
	clc
	adc $fd
	tax
	lda SinTab,x
	sec
	sbc .smpx
	sta $fa
!macro MSpriteXPosCalc .reg , .offset {
	lda $fa
	clc
	adc #.offset
	asl
	sta .reg
	ror $ff
}
	+MSpriteXPosCalc VIC2Sprite0X , 0
	+MSpriteXPosCalc VIC2Sprite1X , 20
	+MSpriteXPosCalc VIC2Sprite2X , 40
	+MSpriteXPosCalc VIC2Sprite3X , 60
	+MSpriteXPosCalc VIC2Sprite4X , 80
	+MSpriteXPosCalc VIC2Sprite5X , 100
	+MSpriteXPosCalc VIC2Sprite6X , 120
	+MSpriteXPosCalc VIC2Sprite7X , 140
	lda $ff
	sta VIC2SpriteXMSB
	rts

!align 255,0
WaitRaster
.wl1
	cmp VIC2Raster
	bne .wl1
	rts

Delay1
.delay
	dex
	bne .delay
	rts

Delay2
.delay2
!for .i , 5 {
	nop
}
	dex
	bne .delay2
	rts

Delay3
.delay3
!for .i , 2 {
	nop
}
	dex
	bne .delay3
	rts
!if >* != >WaitRaster {
	!error "WaitRaster to here must be in the same page"
}

kStartLineDelay = 65

!zn
RenderMegaLine
	ldx #kStartLineDelay
	jsr Delay2

!for .i , 7 {
	nop
}
	bit $02

;	ldy #0

!for .i , 160 {
	lda ($fb),y					; 5+	; 5
	sta VIC2ScreenColour		; 4		; 9
	iny							; 2		; 11
	nop							; 2		; 13
	bit $44						; 3		; 16
}
	+MByteValueToAddress_A VIC2Colour_Blue , VIC2ScreenColour

	rts
	
!zn
RenderMegaLineBad
	ldx #kStartLineDelay
	jsr Delay2

!for .i , 6 {
	nop
}

;	ldy #0

!for .i , 160 {
	lda ($fb),y					; 5+	; 5
	sta VIC2ScreenColour		; 4		; 9
	iny							; 2		; 11
	nop							; 2		; 13
!if (.i & 3) = 0 {
	nop							; 2		; 15
} else {
	bit $44						; 3		; 16
}
}
	+MByteValueToAddress_A VIC2Colour_Blue , VIC2ScreenColour

	rts

!align 255 , 0
SinTab
!for .i , 256 {
	!by 64 + (64 * cos( (float(.i) / 128) * 3.14159265 ))
}
SinTab2
!for .i , 256 {
	!by 32 + (32 * sin( (float(.i) / 128) * 3.14159265 ))
}

!source "features\MinPlay_SID.a"
!source "ApexMusic.a"

* = $800
MegaScreenData
!if 1 {
!bin "c:\temp\out.bin" ,, 4
}

!if 0 {
!for .y , 200 {
* = MegaScreenData + ((.y-1) * $100)
!bin "c:\temp\out.bin" ,160, 4 + ((.y-1) * 160)
}
}

!if 0 {
!for .y , 200 {
!for .x , 256 {
;	!by <(.x + .y)
	!by <(.x)
}
}
}
